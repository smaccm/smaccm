package OSAS
public
  with DATA_TYPES;
  with Base_Types;

system OSAS_Sys
  features
    acts_in : in data port DATA_TYPES::Actuators.impl;
    acts_ret : in data port DATA_TYPES::Actuators.impl;
    acts_claw_fail : in data port DATA_TYPES::BoolActuators.impl;
    acts_isas_fail : in data port DATA_TYPES::BoolActuators.impl;
    acts_valid : in data port DATA_TYPES::NumActuators.impl;
    acts_ccdl_num_fail : in data port DATA_TYPES::NumActuators.impl;
    
    acts_out : out data port DATA_TYPES::Actuators.impl;
	acts_fail : out data port DATA_TYPES::BoolActuators.impl;
     
    sovs : out data port DATA_TYPES::ACTUATORS_SOV_Data.impl; 
    
    ccdl_frame_count: in data port Base_Types::Integer;
    ccdl_failed: out data port Base_Types::Boolean;
    
    num_valid_acts: in data port DATA_TYPES::NumActuators.impl;
    
  annex agree {**
  	
  	eq fail_counts : DATA_TYPES::NumActuators.impl;
  	eq latched_failures : DATA_TYPES::BoolActuators.impl;
  	eq act_gains : DATA_TYPES::NumActuators.impl;
  	eq act_claw_fails : DATA_TYPES::BoolActuators.impl;
  	eq osas_failed : bool;
  	  	
  	--For each actuator, OSAS shall determine whether the actuator 
    --command return value is within 2.5% of actuator command from 
    --the previous frame, and if not shall increment a fault counter 
    --for that actuator command.
    
    --OSAS shall set the actuator command fault counter to 0 (zero) 
    --if the fault clears within 5 frames.
    guarantee "OSAS-S-100 and OSAS-S-110":
      OSAS_S_100_110.req(acts_out, acts_ret, fail_counts); 
    	
    --shall latch the actuator command FAILED if the fault 
    --persists for 5 consecutive frames.
    guarantee "OSAS-S-120":
      OSAS_S_120.req(acts_out, acts_ret, latched_failures); 
    	
    --When an actuator command has been latched failed, OSAS shall set 
    --that actuator command to 0 (zero).
    guarantee "OSAS-S-140" : 
      OSAS_S_140.req(acts_out, latched_failures); 
    
    --OSAS shall compare the current CCDL frame counter to the 
    --previous value to determine if CCDL has had a failure.
    
    --If the CCDL frame counter fails to increment, or does 
    --not increment by 1 (one) each frame, OSAS shall declare the CCDL FAILED.
    guarantee "OSAS-S-150 and OSAS-S-160" : true ->
      ccdl_failed = (ccdl_frame_count != pre(ccdl_frame_count) + 1);
      
    --If the local CCDL has failed, OSAS shall set the local
    --actuator command gain to 1 (one).
    guarantee "OSAS-S-170" :
      OSAS_S_170.req(act_gains, ccdl_failed);
      
    --OSAS shall compute the actuator command gain as the ratio
    --of the total number of command channels to the number of
    --valid command channels (i.e. 4/(number of valid command channels).
    guarantee "OSAS-S-180" :
      OSAS_S_180.req(act_gains, num_valid_acts, ccdl_failed);
    
    guarantee "OSAS-S-190, OSAS-S-240, and OSAS-S-250" : 
      OSAS_S_190_240_250.req(acts_in, acts_out, act_gains, act_claw_fails, osas_failed);
    
    	
  **};
end OSAS_Sys;


end OSAS;