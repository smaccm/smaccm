package array
public
	with Base_Types;
	with Data_Model;
	with SW_Properties;
	
	data ranged_type extends Base_Types::Integer
		properties
			Data_Model::Integer_Range => SW_Properties::LOWER .. SW_Properties::UPPER;
	end ranged_type;
	
	data struct_with_array
	end struct_with_array;
	
	data implementation struct_with_array.i
		subcomponents
			array : data Base_Types::Integer[4];
			ranged : data ranged_type;
	end struct_with_array.i;
	
	data array_type
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Integer));
			Data_Model::Dimension => (4);
	end array_type;
	
	data simple_struct
	end simple_struct;
	
	data implementation simple_struct.i
		subcomponents
			val : data Base_Types::Integer;
	end simple_struct.i;
	
	data implementation simple_struct.i2
		subcomponents
			val : data Base_Types::Integer;
			array : data Base_Types::Integer[10];
			booga: data struct_with_array.i;
	end simple_struct.i2;
	
	data simple_struct_array
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (simple_struct.i));
			Data_Model::Dimension => (4);
	end simple_struct_array;
	
	data simple_int_array
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Integer));
			Data_Model::Dimension => (4);
	end simple_int_array;
		
	data simple_struct_array_DIM
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (simple_struct.i));
			Data_Model::Dimension => (SW_Properties::MAP_DIMENSION);
	end simple_struct_array_DIM;
	
	annex agree {**
--		-- Type Error: cannot assign structure to integer literal
--		fun array_test0(t : struct_with_array.i) : bool =
--			t = 10;
--			
--		-- Type Error: cannot assign array to integer literal
--		fun array_test1(t : struct_with_array.i) : bool =
--			t.array = 10;
		
		-- Correct: assign integer array element to integer literal
		
		fun array_foo(t : struct_with_array.i) : int[4] = t.array;
		
		fun array_test2(t : struct_with_array.i) : bool = 
			t.array[0] = 10;
			
		fun array_test3(t : array_type) : bool = 
			t[2] = 10;

		fun array_test4(t : array_type) : bool =
			forall i in indices(t), t[i] < 10;
			
		fun array_test5(t : simple_struct_array) : bool =
			forall i in t, i.val < 10;
			
		fun array_test6(t : simple_struct_array) : bool =
			forall i in indices(t), (t[i]).val < 10;
			
		fun support_array_test7(x : simple_struct.i) : bool = x.val < 10;
		
		fun array_test7(t : simple_struct_array) : bool = 
			forall i in t, support_array_test7(i);
			
		fun array_test8(t : simple_struct_array) : bool = 
			forall i in indices(t), support_array_test7(t[i]);
		
		fun array_test9(t : simple_struct_array_DIM) : bool =
			forall i in indices(t), support_array_test7(t[i]);
			
		fun array_test10(t : simple_struct.i2) : bool =
			forall i in indices(t.array), (t.array)[i] < 10;
			
		fun array_test11(t : simple_struct.i2) : bool =
			forall i in t.array, i < 10;
			
		fun tll0(x: int) : int = SW_Properties::MAX_ALTITUDE;
		
		fun tll1(x: int) : real = SW_Properties::MAX_LATTITUDE;
			
		fun tll2(arr: simple_int_array) : bool = arr = [|5, 5, 5, 5|] ;
		
        fun ranged_type_test0(r : ranged_type) : bool = 
			r != SW_Properties::MAX_ALTITUDE;
			
		fun ranged_type_test1(r : ranged_type) : bool =
			r != 10;
				
		fun ranged_type_test2(r : struct_with_array.i) : bool =
			r.ranged < 10;
	**};

	thread CompTest
			
		annex agree {**
				
--			eq qq: int[190,200];
--			eq ww: int[180,181];
--			eq bb : bool = ww != qq;
--			
			eq a0 : int = 1;
			fun idnt(x: int) : int = x;
			fun something(xs: int[3]) : int[3] = xs;
			eq a1 : int[3] = [| -1, 2, 3 |];
			eq b1 : bool = forall ya in a1, ya > 0;
			eq b8 : bool = forall yb in a1, yb > 0;
			eq a2 : int[3] = a1[|0 := 2; 1 := 3 |];
			type roo = struct {g : int, y: int};
			eq r1 : roo = roo {g = 7; y = 8};
			eq i1 : int = r1.y;
			eq r2 : roo = r1{y := 4};
			eq a5 : roo[2] = [| (roo {g = 4; y=2}), (roo {g = 5; y=3}) |];
	
			eq i4 : int = (a5[0]).y;
			
			eq arr1: simple_int_array;
			
--			TODO:
			eq b2 : bool = exists x in a1, (x > 0);
			eq a3 : bool[3] = flatmap q in a1, [|(q > 0)|];
			eq i0 : int = foldl g in a1 into asdf = 0 , asdf + g;
			eq i2 : int = foldr p in a1 into acc = 0 , acc + p;
			eq b3 : bool = forall v in a2, v > 0; 
			eq a4 : int[3] = flatmap i in indices(a3), [| if a3[i] then 1 else 0 |];
			eq i3 : int = a4[0];

			
--			TODO:
--			eq arr2: simple_int_array = foreach aa in arr1, (aa * 4);
--			
--			fun foo1(x: int): int = x;
--			fun foo2(x: int): int = x;
--			assume "array_sorted" : forall ii in indices(arr1), ii = 0 or arr1[ii - 1] <= arr1[ii]; 
----			assume "array_sorted2" : forall ii in indices(arr1), ii = 0 or arr1[ii - 1] <= arr1[ii]; 
--			guarantee "sortednes_preserved" : forall iii in indices(arr2), iii = 0 or arr1[iii - 1] <= arr1[iii]; 

			
		**};
		
	end CompTest;
	
	thread implementation CompTest.i
		subcomponents
			arr1: data simple_int_array;
			arr2: data simple_int_array;
			
		annex agree {**
				
			lemma "flatmap" : not a3[0] and a3[1] and a3[2];
			lemma "sum" : i0 = 4 and i2 = 4;
			lemma "array_stuff" : (forall iqq in arr1, iqq <= 0 or iqq > 0);
--			lemma "array_sorted" : forall iq in indices(arr1), iq = 0 or arr1[iq - 1] <= arr1[iq]; 
		**};
		
	end CompTest.i;
	
end array;
